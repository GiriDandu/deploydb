plugins {
    id 'com.github.jruby-gradle.base' version '0.1.10'
    id 'com.github.jruby-gradle.jar' version '0.1.3'
    id 'com.jfrog.bintray' version '1.0'
    id 'org.asciidoctor.gradle.asciidoctor' version '1.5.1'
}

version = '0.1.0'
group = 'com.github.lookout'
description = 'DeployDB is a tool to provide a single source of truth for artifact-based deployments'

////////////////////////////////////////////////////////////////////////////////
// TASKS
////////////////////////////////////////////////////////////////////////////////
import com.github.jrubygradle.JRubyExec

task spec(type: JRubyExec) {
    group 'JRuby'
    description 'Run the RSpec examples'
    jrubyArgs '-S'
    script 'rspec'
}


task cucumber(type: JRubyExec) {
    group 'JRuby'
    description 'Run the Cucumber features'
    jrubyArgs '-S'
    script 'cucumber'
}


asciidoctor {
    sourceDir 'src/asciidoc'
    outputDir 'docs'
    attributes 'toc': 'right',
                'source-highlighter': 'coderay',
                'toc-title': 'Table of Contents'
}


task publishDocs(type: Exec) {
    group 'Publishing'
    description 'Publish the Asciidoctor docs to gh-pages'
    commandLine 'git'
    args 'subtree', 'push', '--prefix', 'docs/html5', 'origin', 'gh-pages'
    dependsOn asciidoctor
}


// Add some task dependencies to the `check` task so Travis CI will run the
// right tests when code is committed
check.dependsOn spec, cucumber, asciidoctor
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// DEPENDENCY MANAGEMENT
////////////////////////////////////////////////////////////////////////////////

dependencies {
    gems 'rubygems:faraday:0.9.0+'
    gems 'rubygems:sinatra:1.4.5+'

    jrubyExec 'rubygems:rspec:3.1.0+'
    jrubyExec 'rubygems:cucumber:1.3.17+'
    jrubyExec 'rubygems:timecop:0.7.1+'
    jrubyExec 'rubygems:liquid:3.0.0+'
    jrubyExec 'rubygems:rack-test:0.6.2+'
}

configurations {
  // Make our jrubyExec configuration a superset of the 'gems' configuration so
  // we automatically get those "basic dependencies"
  jrubyExec.extendsFrom gems
}
////////////////////////////////////////////////////////////////////////////////

// vim: ft=groovy
