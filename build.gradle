plugins {
    id 'com.jfrog.bintray' version '1.0'
    id 'org.asciidoctor.gradle.asciidoctor' version '1.5.1'
    id 'com.github.johnrengelman.shadow' version '1.2.0'
    id "com.github.samueltbrown.cucumber" version "0.8"
}

apply plugin: 'groovy'

version = '0.1.0'
group = 'com.github.lookout'
description = 'DeployDB is a tool to provide a single source of truth for artifact-based deployments'

////////////////////////////////////////////////////////////////////////////////
// DEPENDENCY MANAGEMENT
////////////////////////////////////////////////////////////////////////////////

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.7.1+'
    compile 'org.flywaydb:flyway-core:3.1+'
    compile 'com.h2database:h2:1.3.+'

    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'cglib:cglib-nodep:2.2.+'

    cucumberCompile 'info.cukes:cucumber-groovy:1.2.+'
}

repositories {
    jcenter()
    mavenCentral()
}
////////////////////////////////////////////////////////////////////////////////


////////////////////////////////////////////////////////////////////////////////
// TASKS
////////////////////////////////////////////////////////////////////////////////

cucumber {
    formats = ['asyougo']
    glueDirs = ['src/cucumber/groovy/step_definitions']
    featureDirs = ['features']
}
project.tasks.cucumber.doFirst {
    // NOTE: This hack exists because it appears that the cucumber gradle
    // plugin we're using does not properly create its own output dirs
    file("${buildDir}/resources/cucumber").mkdirs()
}
// Need a goofy way to refer to the task, see:
// https://github.com/samueltbrown/gradle-cucumber-plugin/issues/38
check.dependsOn project.tasks.cucumber


asciidoctor {
    sourceDir 'src/asciidoc'
    outputDir 'docs'
    attributes 'toc': 'right',
                'source-highlighter': 'coderay',
                'toc-title': 'Table of Contents'
}
check.dependsOn asciidoctor
////////////////////////////////////////////////////////////////////////////////


////////////////////////////////////////////////////////////////////////////////
// PUBLISHING TASKS
////////////////////////////////////////////////////////////////////////////////

task publishDocs(type: Exec) {
    group 'Publishing'
    description 'Publish the Asciidoctor docs to gh-pages'
    commandLine 'git'
    args 'subtree', 'push', '--prefix', 'docs/html5', 'origin', 'gh-pages'
    dependsOn asciidoctor
}


bintray {
    user = project.bintrayUser
    key = project.bintrayKey
    publish = true
    dryRun = false
    configurations = ['archives']

    pkg {
        userOrg = 'lookout'
        repo = 'systems'
        name = 'DeployDB'
        labels = []

        version {
            name = project.version
            vcsTag = "v${project.version}"
            desc = project.description
        }
    }
}
bintrayUpload.dependsOn assemble
////////////////////////////////////////////////////////////////////////////////


// vim: ft=groovy
